# This CONSTRUCT query annotates WikiPathways Directed Interactions. 
PREFIX rdf:     <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX dc:      <http://purl.org/dc/elements/1.1/>
PREFIX dcterms:  <http://purl.org/dc/terms/>
PREFIX gpml:    <http://vocabularies.wikipathways.org/gpml#>
PREFIX wp:      <http://vocabularies.wikipathways.org/wp#>
PREFIX xsd:     <http://www.w3.org/2001/XMLSchema#>

CONSTRUCT {
	?baseline wp:isNecessaryStimulatedBy ?datanode .
}
FROM <http://rdf.wikipathways.org/>
WHERE {
	?interactionline gpml:arrowHead "mim-necessary-stimulation"^^xsd:string .
 	?interactionline dcterms:isPartOf ?pathway .
        
    # baseline affected
    ?baseline gpml:graphid ?edgeGraphId .
    ?baseline dcterms:isPartOf ?pathway .

	# DataNode 1   	
   	?datanode gpml:graphid ?dn1GraphId .
   	?datanode rdf:type gpml:DataNode .
   	?datanode dcterms:isPartOf ?pathway .

    ?interactionline gpml:graphref ?dn1GraphId .
    ?interactionline gpml:graphref ?edgeGraphId .	
    ?interactionline gpml:arrowTowards ?edgeGraphId .
}